buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://maven.google.com" }
        maven { url "https://jitpack.io" }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
group='com.github.EricssonBroadcastServices'

ext {
    exoPlayerVersion = 'r2.5.0'
    releasePlayerName = 'EMPPlayer'
    releaseArtifact = 'fluxplayer-playback'
    releaseVersion = 'v0.71.0'
    releaseDescription = 'The EMP Player library playback module.'
    libraryTag = 'master-SNAPSHOT'
    lightweight = { -> return false }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            resValue "string", "exoplayer_version", project.ext.exoPlayerVersion
            resValue "string", "emplayer_version", project.ext.releaseVersion
            resValue "string", "emplayer_name", project.ext.releasePlayerName
        }
        release {
            resValue "string", "exoplayer_version", project.ext.exoPlayerVersion
            resValue "string", "emplayer_version", project.ext.releaseVersion
            resValue "string", "emplayer_name", project.ext.releasePlayerName
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
    // Lightweight build removes EMP factory
    if (lightweight()) {
        sourceSets {
            main {
                java {
                    exclude '**/factories/EMPPlayerFactory.java'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:26.0.1"
    compile "com.android.support:design:26.0.1"
    compile "com.google.android.exoplayer:exoplayer:${project.ext.exoPlayerVersion}"
    compile 'com.android.support:support-core-utils:26.0.1'
    compile 'joda-time:joda-time:2.9.4'
    if (project.hasProperty('hasParentProject')) {
        compile project(':libraries:utilities')
        if (lightweight() == false) {
            compile project(':libraries:analytics')
        }
    }
    else {
        compile "com.github.EricssonBroadcastServices:AndroidClientUtilities:${project.ext.libraryTag}"
        if (lightweight() == false) {
            compile "com.github.EricssonBroadcastServices:AndroidClientAnalytics:${project.ext.libraryTag}"
        }
    }
    testCompile 'junit:junit:4.12'
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}